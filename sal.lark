%import common.CNAME
%import common.WS
%import common.NUMBER
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.ESCAPED_STRING

DECLARE: "declare"
DEFINE: "define"
VA_ARGS: "..."

IDENTIFIER: "$"? CNAME

start: statement*

code_block: "{" statement* "}"

?statement: proc | variable | reassignment | proc_call | return_statement

return_statement: "return" expression

proc_call: "call" type_list? expression "(" (expression ("," expression)*)? ")"

type_list: "(" (expression ("," expression)*)? ")"

variable: modifier* "var" IDENTIFIER type assign?

reassignment: expression assign

assign: "=" expression

?expression: integer | addr_of | IDENTIFIER | ESCAPED_STRING | proc_call | ("type" type) | dereference

dereference: "*" expression

addr_of: "&" expression

integer: NUMBER

proc: template? modifier* "proc" IDENTIFIER proc_params type code_block?

template: "template" "(" IDENTIFIER ("," IDENTIFIER)* ")"

modifier: DECLARE | DEFINE

proc_params_pair: (IDENTIFIER type) | VA_ARGS

proc_params: "(" (proc_params_pair ("," proc_params_pair)*)? ")"

?type: plain_type | ptr_type | proc_type

ptr_type: "*" type

plain_type: IDENTIFIER

proc_type: "proc" proc_params type

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT

