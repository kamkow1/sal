%import common.CNAME -> IDENTIFIER
%import common.WS
%import common.NUMBER

DECLARE: "declare"
DEFINE: "define"
COMMENT: /\/\/[^\n]*/

start: statement*

code_block: "{" statement* "}"

?statement: proc | variable | reassignment

variable: modifier* "var" IDENTIFIER type assign?

reassignment: IDENTIFIER assign

assign: "=" expression

?expression: integer | addr_of | IDENTIFIER

addr_of: "&" expression

integer: NUMBER

proc: modifier* "proc" IDENTIFIER proc_params type code_block?

modifier: DECLARE | DEFINE

proc_params_pair: IDENTIFIER type

proc_params: "(" (proc_params_pair ("," proc_params_pair)*)? ")"

?type: plain_type | ptr_type | proc_type

ptr_type: "*" type

plain_type: IDENTIFIER

proc_type: "proc" proc_params type

%ignore WS
%ignore COMMENT

