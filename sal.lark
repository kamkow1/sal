%import common.CNAME -> IDENTIFIER
%import common.WS
%import common.NUMBER
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.ESCAPED_STRING

DECLARE: "declare"
DEFINE: "define"
VA_ARGS: "..."

start: statement*

code_block: "{" statement* "}"

?statement: proc | variable | reassignment | proc_call

proc_call: "call" expression "(" (expression ("," expression)*)? ")"

variable: modifier* "var" IDENTIFIER type assign?

reassignment: IDENTIFIER assign

assign: "=" expression

?expression: integer | addr_of | IDENTIFIER | ESCAPED_STRING | proc_call | ("type" type)

addr_of: "&" expression

integer: NUMBER

proc: modifier* "proc" IDENTIFIER proc_params type code_block?

modifier: DECLARE | DEFINE

proc_params_pair: (IDENTIFIER type) | VA_ARGS

proc_params: "(" (proc_params_pair ("," proc_params_pair)*)? ")"

?type: plain_type | ptr_type | proc_type

ptr_type: "*" type

plain_type: IDENTIFIER

proc_type: "proc" proc_params type

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT

